///$tab Main
SET ThousandSep=',';
SET DecimalSep='.';
SET MoneyThousandSep=',';
SET MoneyDecimalSep='.';
SET MoneyFormat='$ ###0.00;-$ ###0.00';
SET TimeFormat='h:mm:ss TT';
SET DateFormat='M/D/YYYY';
SET TimestampFormat='M/D/YYYY h:mm:ss[.fff] TT';
SET FirstWeekDay=6;
SET BrokenWeeks=1;
SET ReferenceDay=0;
SET FirstMonthOfYear=1;
SET CollationLocale='en-US';
SET CreateSearchIndexOnReload=1;
SET MonthNames='Jan;Feb;Mar;Apr;May;Jun;Jul;Aug;Sep;Oct;Nov;Dec';
SET LongMonthNames='January;February;March;April;May;June;July;August;September;October;November;December';
SET DayNames='Mon;Tue;Wed;Thu;Fri;Sat;Sun';
SET LongDayNames='Monday;Tuesday;Wednesday;Thursday;Friday;Saturday;Sunday';
SET NumericalAbbreviation='3:k;6:M;9:G;12:T;15:P;18:E;21:Z;24:Y;-3:m;-6:Î¼;-9:n;-12:p;-15:f;-18:a;-21:z;-24:y';

///$tab MS Teams
//let vTeamsWebhook = 'https://notitia.webhook.office.com/webhookb2/6a06b6a0-735d-461f-8605-f32fc64afe4e@71a3136a-a5e2-4bc9-bef9-82657c5d4d7f/IncomingWebhook/f6c55ee8ea404f0e93dc65dbdabce743/c266e620-2642-4099-9014-6248b502d33b';
let vTeamsWebhook = 'https://notitia.webhook.office.com/webhookb2/6a06b6a0-735d-461f-8605-f32fc64afe4e@71a3136a-a5e2-4bc9-bef9-82657c5d4d7f/IncomingWebhook/8927c09f62e04d83b354306d4cf214f8/d159148f-4c91-470e-93e7-833aa97addc0';
Sub CallTeamsAPI(vL.WebHookURL2, vL.Body)
    let vL.TeamsMessage = Replace('$(vL.Body)', '"', Chr(39));
    
    LIB CONNECT TO 'PostMessageToTeams';

    RestConnectorMasterTable:
    SQL SELECT 
        "col_1"
    FROM CSV(header off, delimiter ",", quote """") "CSV_source"
    WITH CONNECTION(
        Body "$(vL.TeamsMessage)",
        Url "$(vL.WebHookURL2)"
    );

    [CSV_source]:
    LOAD
        [col_1] AS [col_1]
    RESIDENT RestConnectorMasterTable;

    // Clean up
    Drop Tables RestConnectorMasterTable, CSV_source;
    set vL.WebHookURL2 = ;
    set vL.Body = ;
    set vL.TeamsMessage = ;
End Sub

Sub PostToTeamsChannel_GenericMessage(vL.WebHookURL, vL.Message)
    let vL.Tmp = '{"text": "$(vL.Message)"}';
    Call CallTeamsAPI('$(vL.WebHookURL)', '$(vL.Tmp)');
    
    set vL.Tmp = ;
    set vL.WebHookURL = ;
    set vL.Message = ;
End Sub


Sub PostToTeamsChannel_AppNotification(vL.AppBaseURL, vL.WebHookURL, vL.Title, vL.SubTitle, vL.FactTitle1, vL.FactValue1, vL.FactTitle2, vL.FactValue2)
    
    let vL.Summary = 'Info message from Qlik Sense';

    let vL.IconURL = 'https://openclipart.org/image/300px/svg_to_png/215532/1425710397.png';
	
    let vL.AppURL = '$(vL.AppBaseURL)' & DocumentName();
    let vL.Tmp = '{"@type": "MessageCard","@context": "http://schema.org/extensions","themeColor": "0076D7","summary": "$(vL.Summary)","sections": [{"activityTitle": "![TestImage](https://47a92947.ngrok.io/Content/Images/default.png)$(vL.Title)","activitySubtitle": "$(vL.SubTitle)","activityImage": "$(vL.IconURL)","facts": [ {"name": "$(vL.FactTitle1)", "value": "$(vL.FactValue1)"}, {"name": "$(vL.FactTitle2)", "value": "$(vL.FactValue2)"}], "markdown": true}], "potentialAction": [{"@type": "OpenUri", "name": "Open the app", "targets": [{"os": "default", "uri": "$(vL.AppURL)"}]}]}';
    
    Call CallTeamsAPI('$(vL.WebHookURL)', '$(vL.Tmp)');

	set vL.AppBaseURL = ;
    set vL.WebHookURL = ;
    set vL.Title = ;
    set vL.SubTitle = ;
    set vL.FactTitle1 = ;
    set vL.FactValue1 = ;
    set vL.FactTitle2 = ;
    set vL.FactValue2 = ;
    set vL.Summary = ;
    set vL.IconURL = ;
    set vL.AppURL = ;
    set vL.Tmp = ;
End Sub

let vMsg = '<h1>Posting message to Teams from Qlik Sense</h1> 

A basic message from <b>Qlik Sense</b>
';

Call PostToTeamsChannel_GenericMessage('$(vTeamsWebhook)', '$(vMsg)');

let vSenseBaseURL = 'https://uavzecaz8dfeey0.ap.qlikcloud.com/sense/app/9148cfa2-ae2b-4fe6-a299-e3532d5183a8/sheet/b7a430e5-5adc-47f4-9a47-16650311d75f/';
let vTitle = 'Daily sales metrics in Qlik Sense have been updated';
let vSubtitle = 'Click the button to open the app';
let vFactTitle1 = 'Total sales ' & Date(Today()-1, 'YYYY-MM-DD') & ' (kEUR)';
let vFactValue1 = '125';
let vFactTitle2 = 'Change compared to 7-day average)';
let vFactValue2 = '44.6%';


Call PostToTeamsChannel_AppNotification('$(vSenseBaseURL)', '$(vTeamsWebhook)', '$(vTitle)', '$(vSubTitle)', '$(vFactTitle1)', '$(vFactValue1)', '$(vFactTitle2)', '$(vFactValue2)');
///$tab Data Demo
Characters:
Load Chr(RecNo()+Ord('A')-1) as Alpha, RecNo() as Num autogenerate 26;
 
ASCII:
Load 
 if(RecNo()>=65 and RecNo()<=90,RecNo()-64) as Num,
 Chr(RecNo()) as AsciiAlpha, 
 RecNo() as AsciiNum
autogenerate 255
 Where (RecNo()>=32 and RecNo()<=126) or RecNo()>=160 ;
 
Transactions:
Load
 TransLineID, 
 TransID,
 mod(TransID,26)+1 as Num,
 Pick(Ceil(3*Rand1),'A','B','C') as Dim1,
 Pick(Ceil(6*Rand1),'a','b','c','d','e','f') as Dim2,
 Pick(Ceil(3*Rand()),'X','Y','Z') as Dim3,
 Round(1000*Rand()*Rand()*Rand1) as Expression1,
 Round(  10*Rand()*Rand()*Rand1) as Expression2,
 Round(Rand()*Rand1,0.00001) as Expression3;
Load 
 Rand() as Rand1,
 IterNo() as TransLineID,
 RecNo() as TransID
Autogenerate 1000
 While Rand()<=0.5 or IterNo()=1;